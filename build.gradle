plugins {
    id 'io.freefair.lombok' version '5.1.0' apply false
}

task copyHooks(type: Copy) {
    from(file("./hooks/"))
    into(file("./.git/hooks/"))
}

task compileJava {
    dependsOn copyHooks
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    group 'de.gleyder.admiral'

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = "SNAPSHOT"

                from components.java
            }
        }
    }
}
